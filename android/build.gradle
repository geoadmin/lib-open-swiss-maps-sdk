buildscript {
	ext.kotlin_version = "1.4.20"
	repositories {
		google()
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:4.1.2'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

repositories {
	google()
	jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 30

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 30
		versionCode 110
		versionName "1.1.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles 'consumer-rules.pro'
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_shared"
				cppFlags "-std=c++17 -frtti -fexceptions"
			}
		}
	}

	buildTypes {
		debug {
			buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
		}
		release {
			buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
		}
	}

	externalNativeBuild {
		cmake {
			path "CMakeLists.txt"
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
				srcDir '../bridging/android/java'
			}
		}
	}

	buildFeatures {
		prefab true
		prefabPublishing true
	}

	prefab{
		openswissmaps{
			headers ".cpp_includes"
			name "openswissmaps"
			version 110
		}
	}

	packagingOptions {
		exclude("**/libmapscore.so")
		exclude('**/libc++_shared.so')
	}
}

task copyHeaders(type: Exec){
	commandLine './copy_all_header_files_to_include_folder.sh'
}
preBuild.dependsOn copyHeaders

task androidSourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}
afterEvaluate {
	publishing {
		publications {
			"sdkRelease"(MavenPublication) {
				from components.release
				groupId 'ch.admin.geo.openswissmaps'
				artifactId 'openswissmaps-android'
				version android.defaultConfig.versionName + readPropertyWithDefault('bintrayVersionSuffix', '')
				artifact androidSourcesJar
			}
		}
	}
}

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

bintray {
	user = readProperty('bintrayUser')
	key = readProperty('bintrayApikey')
	publications = ['sdkRelease']
	publish = true
	pkg {
		repo = 'openswissmaps'
		name = 'openswissmaps'
		userOrg = 'openswissmaps'
		licenses = ['MPL-2.0']
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation "androidx.annotation:annotation:1.1.0"
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

	api 'io.openmobilemaps.mapscore:mapscore-android:1.2.0-dev-15'

	testImplementation 'junit:junit:4.13.1'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
